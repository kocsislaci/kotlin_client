plugins {
	id 'com.android.library'
	id 'org.jetbrains.kotlin.android'
	id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.30'
	id 'maven-publish'
	id 'com.diffplug.spotless' version '6.8.0'
}

android {
	compileSdk 32
	defaultConfig {
		minSdk 26
		targetSdk 32
	}

	buildTypes {
		debug {
			def localProperties = new Properties()
			localProperties.load(new FileInputStream(rootProject.file("local.properties")))
			buildConfigField(
					"String",
					"PaylikeMerchantApiKey",
					(localProperties.containsKey("PaylikeMerchantApiKey")
					? localProperties.getProperty("PaylikeMerchantApiKey")
					: "new String()"
					)
					)
		}
		release {
			minifyEnabled false
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = '1.8'
	}
}

dependencies {
	testImplementation 'junit:junit:4.13.2'
	api 'org.jetbrains.kotlinx:kotlinx-datetime:0.4.0'
	api 'com.github.paylike:kotlin_request:1.0.2'
	api 'com.github.paylike:kotlin_money:1.0.2'
}

spotless {
	kotlin {
		target 'src/**/*.kt', 'src/**/*.kts'
		ktfmt().kotlinlangStyle()
		trimTrailingWhitespace()
		endWithNewline()
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}
build.dependsOn 'spotlessApply'

afterEvaluate {
	publishing {
		publications {
			release(MavenPublication) {
				from components.release

				groupId = 'com.github.paylike'
				artifactId = 'kotlin_client'
				version = '1.0.0'
			}
		}
	}
}
